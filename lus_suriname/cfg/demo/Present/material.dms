//**********************************************************************************************************************************************************************************************
//**                                                                                                                                                                                          **
//**                                                Configuration written for Suriname Land Use Modelling project, 2012                                                                       **
//**                                                                                                                                                                                          **
//**********************************************************************************************************************************************************************************************
//**                                                                                                                                                                                          **
//**                                           configuration file with configuration of materials used as suitability factors                                                                 **
//**                                                                                                                                                                                          **
//**********************************************************************************************************************************************************************************************
container material
{
	container sources: IsHidden = "True"
	{
		unit<uint32> mining
		:	DialogData      = "geometry"
		,	DialogType      = "Map"
		,	StorageName     = "%ProjDir%/data/factor/mining_utm.dbf"
		,	StorageReadOnly = "True"
		{
			attribute<geography/SurinameCoord>        geometry (polygon): StorageName = "%ProjDir%/data/factor/mining_utm.shp";
			attribute<uint32>                         DN;
			attribute<Classifications/mining_classes> type: expr = "rlookup(DN, Classifications/mining_classes/EK)", DisableStorage = "True";
		}

		unit<uint32> timber
		:	DialogData      = "geometry"
		,	DialogType      = "Map"
		,	StorageName     = "%ProjDir%/data/factor/timber.dbf"
		,	StorageReadOnly = "True"
		{
			attribute<geography/SurinameCoord> geometry (polygon): StorageName = "%ProjDir%/data/factor/timber.shp";
		}

	}
	container preperation: IsHidden = "True"
	{
		container mining: FreeData = "True"
		{
			attribute<mining_classes> type          (Geography/Suriname250mGrid): expr = "sources/mining/type[poly2grid(sources/mining/geometry,Geography/Suriname250mGrid)]";

			attribute<bool> bauxiet_zonder_outlines (Geography/Suriname250mGrid): expr = "Geography/Suriname250mGrid/isSuriname && type == value(0, mining_classes)";
			attribute<bool> goud_zonder_outlines    (Geography/Suriname250mGrid): expr = "Geography/Suriname250mGrid/isSuriname && (type == value(1, mining_classes) || type == value(2, mining_classes))";

			attribute<float32> bauxiet_potential    (Geography/Suriname250mGrid): expr = "potential(float32(bauxiet_zonder_outlines), Geography/DistMatrices/p1000m/PotRange/RelWeightSqrt)";
			attribute<float32> goud_potential       (Geography/Suriname250mGrid): expr = "potential(float32(goud_zonder_outlines)   , Geography/DistMatrices/p1000m/PotRange/RelWeightSqrt)";
			attribute<bool>    wordtBauxiet         (Geography/Suriname250mGrid): expr = "bauxiet_potential > goud_potential";

			attribute<bool>    bauxiet              (Geography/Suriname250mGrid): expr = "bauxiet_zonder_outlines || (type == value(3, mining_classes) && wordtBauxiet)";
			attribute<bool>    goud                 (Geography/Suriname250mGrid): expr = "goud_zonder_outlines    || (type == value(3, mining_classes) && !wordtBauxiet)";

		}
		attribute<bool> Timber (Geography/Suriname250mGrid): expr = "Geography/Suriname250mGrid/isSuriname && IsDefined(Poly2Grid(sources/timber/geometry,Geography/Suriname250mGrid))";
	}
	attribute<Classifications/YesNo> bauxiet (Geography/Suriname250mGrid): expr = "preperation/mining/bauxiet";
	attribute<Classifications/YesNo> goud    (Geography/Suriname250mGrid): expr = "preperation/mining/goud";
	attribute<Classifications/YesNo> timber  (Geography/Suriname250mGrid): expr = "preperation/timber";
}