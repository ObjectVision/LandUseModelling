//**********************************************************************************************************************************************************************************************
//**                                                                                                                                                                                          **
//**                                                Configuration written for Suriname Land Use Modelling project, 2012                                                                       **
//**                                                                                                                                                                                          **
//**********************************************************************************************************************************************************************************************
//**                                                                                                                                                                                          **
//**                                           configuration file with configuration of physical data used as suitability factors                                                             **
//**                                                                                                                                                                                          **
//**********************************************************************************************************************************************************************************************
container physical
{
	container sources: IsHidden = "True"
	{
		attribute<m> elevation (Geography/Suriname250mGridSrc)
		:	StorageName = "%ProjDir%/data/Factor/hoogtekaart.tif"
		,	StorageReadOnly = "True"
		,	cdf             = "Classifications/m_elevation/ClassBreaks";
		
		attribute<degrees> slope (Geography/Suriname250mGridSrc)
		:	StorageName     = "%ProjDir%/data/Factor/rastert_slope_g2.tif"
		,	StorageReadOnly = "True";

		unit<uint32> soil
		:	DialogData      = "geometry"
		,	DialogType      = "Map"
		,	StorageName     = "%ProjDir%/data/factor/Bodem_su_UTM.dbf"
		,	StorageReadOnly = "True"
		{
			attribute<geography/SurinameCoord> geometry (polygon)
			:	label       = "Soil"
			,	StorageName = "%ProjDir%/data/factor/Bodem_su_UTM.shp"
			{
				parameter<uint32> PenColor
				:	DialogType     = "PenColor"
				,	expr           = "rgb(  0,  0,200)"
				,	DisableStorage = "True";
				parameter<int16>  PenWidth
				:	DialogType     = "PenWidth"
				,	expr           = "int16(1)"
				,	DisableStorage = "True";
			}
			attribute<Classifications/soiltypes> type: expr = "rlookup(DOMSOIL_UN, Classifications/soiltypes/name)", DisableStorage = "True";
			attribute<String>                    DOMSOIL_UN;
		}
	}
	container preperation: IsHidden = "True", FreeData = "False"
	{
		attribute<m> elevation (Geography/Suriname250mGrid)
		:	expr = 
			"MakeDefined("
				" sources/elevation[Geography/Suriname250mGrid/GridSrcNr]"
				",value (0,m)"
			")"; // in water gebieden soms missing, vandaar de MakeDefined

		attribute<degrees> slope (Geography/Suriname250mGrid): expr =
			"MakeDefined("
				" sources/slope[Geography/Suriname250mGrid/GridSrcNr]"
				", value(0,degrees)"
			")"; // in water gebieden soms missing, vandaar de MakeDefined
	
		attribute<degrees> slope_potential (Geography/Suriname250mGrid): expr = 
			"potential("
				"slope, Geography/DistMatrices/p500m/potRange/RelWeightEqual"
			")"; // relatieve slope t.o.v. zijn omgeving
	
		attribute<ratio> slope_relative (Geography/Suriname250mGrid): expr = 
			"rescale("
				" MakeDefined(slope / slope_potential, value(0,ratio))"
				",float32(0)"
				",float32(1)"
			")"; // herschaalde relatieve slope t.o.v. zijn omgeving

		attribute<ratio> slope_relative_in_Suriname (Geography/Suriname250mGrid): expr =
			"Geography/Suriname250mGrid/isSuriname"
				"? slope_relative"
				": value( 0 / 0, ratio)";

		attribute<soiltypes> soil (Geography/Suriname250mGrid): expr = 
			"sources/soil/type[poly2grid(sources/soil/geometry,Geography/Suriname250mGrid)]";
	}
	attribute<m> elevation (Geography/Suriname250mGrid)
	:	expr     = "preperation/elevation"
	,	cdf      = "Classifications/m_elevation/ClassBreaks";

	attribute<degrees>   slope (Geography/Suriname250mGrid): expr = "preperation/slope", FreeData = "False";
	attribute<soiltypes> soil  (Geography/Suriname250mGrid): expr = "preperation/soil",  FreeData = "False";
}
