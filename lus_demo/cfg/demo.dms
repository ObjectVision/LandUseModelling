// /////////////////////////////////////////////////////////////////////////////////////////////////
//                                                                                                //
//                 Configuration Land Use Scanner - Student Edition                               //
//                          using GeoDMS version 14.14 or newer                                   //
//                                                                                                //
//                                July 2004 - June 2024                                           //
//                                                                                                //
//                            Vrije Universiteit Amsterdam                                        //
//                            Spatial Information Laboratory                                      //
//                Dr. Jasper Dekkers & Dr. Eric Koomen & Jip Claassens                            //
//                                                                                                //
// /////////////////////////////////////////////////////////////////////////////////////////////////
//                                                                                                //
// This is the base file for the configuration                                                    //
//                                                                                                //
// Using include-statements, this file refers to the dms-files with:                              //
// - Units                                                                            (Units.dms) //
// - Geography                                                                    (Geography.dms) //
// - Classifications                                                        (Classifications.dms) //
// - Cuurent landuse (landuse, thematic_maps, policy_maps, DistanceDecay) (Current_situation.dms) //
// - Components for determining future landuse                           (ScenarioComponents.dms) //
// - Algorithms procedures (continuous & discrete)                                (Templates.dms) //
// - Simulations for future landuse                                             (Simulations.dms) //
//                                                                                                //
// /////////////////////////////////////////////////////////////////////////////////////////////////

container LUS_Demo: IntegrityCheck = "GeoDMSVersion() >= 16.0"
{
	unit<SPoint> domain := =ModelParameters/ModelResolution == '100m' ? 'Geography/rdc_100m' : 'Geography/rdc_500m', IsHidden = "true";

	container ModelParameters : using = "Units"
	{
		parameter<string>    ModelResolution     := '100m'; // '100m' '500m'
		parameter<bool>      EnableBackgroundMap := TRUE; // TRUE FALSE
		unit<UInt32>         RegioUnit           := Geography/RegionalUnits/RandstadIntermediatePeripheral;
		
		container Advanced : IsHidden = "true"
		{
			parameter<Year>          StartYear       := 2020[year];
			parameter<Year>          ModelYear       := 2040[year];
			parameter<YearDiffRange> ClaimPeriod     := (ModelYear - StartYear)[YearDiffRange];
			parameter<IterRange>     NrOfIterations  := 30[IterRange];
			parameter<m2_Eur>        BetaFactor      := 1[m2_Eur];
			
			unit<uint8> Scenarios : nrofrows = 4
			{
				attribute<string> name       : ['A1_GE','A2_TM','B1_SE','B2_RC'];
				attribute<string> name_short : ['A1','A2','B1','B2'];
			}
			
		}
	}
	
	#include <Units.dms>
	#include <Geography.dms>
	#include <Classifications.dms>
	#include <Templates.dms>
	#include <Current_situation.dms>
	#include <ScenarioComponents.dms>
	#include <Simulations.dms>
	
	container Final_Results : IsHidden = "true"
	{
		attribute <Classifications/LUclasses> A1_GE_Discr     (domain) := Simulations/A1_GE/Results/Discrete/LandUse/Model;
		attribute <Classifications/LUclasses> A1_GE_Cont      (domain) := Simulations/A1_GE/Results/Continuous/LandUse/predominant;
		attribute <Classifications/LUclasses> A2_TM_Discr     (domain) := Simulations/A2_TM/Results/Discrete/LandUse/Model;
		attribute <Classifications/LUclasses> A2_TM_Cont      (domain) := Simulations/A2_TM/Results/Continuous/LandUse/predominant;
		attribute <Classifications/LUclasses> B1_SE_Discr     (domain) := Simulations/B1_SE/Results/Discrete/LandUse/Model;
		attribute <Classifications/LUclasses> B1_SE_Cont      (domain) := Simulations/B1_SE/Results/Continuous/LandUse/predominant;
		attribute <Classifications/LUclasses> B2_RC_Discr     (domain) := Simulations/B2_RC/Results/Discrete/LandUse/Model;
		attribute <Classifications/LUclasses> B2_RC_Cont      (domain) := Simulations/B2_RC/Results/Continuous/LandUse/predominant;
	}
	
	container ExportSettings : IsHidden = "true"
	{
		container MetaInfo 
		{
			parameter<string> FileName : ='%storageBaseName%.xml';
			parameter<string> FileType : ='xml'; // default is 'ini' but ini files cannot be used well to store multi-line properties such as error messages of inproperly configured properties
			
			container Contents
			{
				container Software
				{
					parameter<string> GeoDMSVersion := string(GeoDmsMajorVersionNumber()) + '.' +  string(GeoDmsMinorVersionNumber())  +'.'  +  string(GeoDmsPatchNumber());
				}
				container Config
				{
					parameter<string> FullName := '=PropValue(this, '+Quote('FullName')+')';
					parameter<string> Expr     := '=PropValue(this, '+Quote('Expr'    )+')';
				}
				container Environment
				{
					parameter<string> Processor    := expand(., '%env:PROCESSOR_IDENTIFIER%');
					parameter<string> User         := expand(., '%env:USERNAME%');
					parameter<string> ComputerName := expand(., '%env:COMPUTERNAME%');
					parameter<string> Date         := expand(., '%env:DATE%');
					parameter<string> Time         := expand(., '%env:TIME%');
				}
			}
		}
	}
}
